buildscript {
    ext {
        springBootVersion = '2.5.3'
    }

    repositories {
        mavenCentral()
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
        classpath "io.spring.gradle:dependency-management-plugin:1.0.11.RELEASE"
        classpath "org.sonarsource.scanner.gradle:sonarqube-gradle-plugin:3.0"
    }
}

ext {
    springCloudVersion = "2020.0.4"
}

ext['log4j2.version'] = '2.17.1'

subprojects {
    apply plugin: 'java'
    apply plugin: 'org.springframework.boot'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'jacoco'
    apply plugin: 'org.sonarqube'

    sourceCompatibility = 11
    group = 'com.yapp'
    version = '1.0.2'

    repositories {
        mavenCentral()
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
        }
    }

    dependencies {
        developmentOnly 'org.springframework.boot:spring-boot-devtools'
        compileOnly 'org.projectlombok:lombok'
        annotationProcessor 'org.projectlombok:lombok'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    configurations {
        compileOnly {
            extendsFrom annotationProcessor
        }
    }

    test {
        useJUnitPlatform()
        testLogging {
            events "failed"
            exceptionFormat "full"
        }
        finalizedBy 'jacocoTestReport'
    }

    jacoco {
        toolVersion = '0.8.7'
    }

    jacocoTestReport {
        dependsOn test // 리포트가 만들어지기 전 테스트 실행되어야 한다.
        reports {
            html.enabled true
            xml.enabled true
            csv.enabled false
        }

        def Qdomains = []
        for (qPattern in "**/QA".."**/QZ") {
            Qdomains.add(qPattern + "*")
        }

        afterEvaluate {
            classDirectories.setFrom(files(classDirectories.files.collect {
                fileTree(dir: it, // 리포트 결과에 제외시킬 디렉토리
                        exclude: ['**/LocalConfig*',
                                  '**/SharefoodApplication*',
                                  '**/OrderByNull*',
                                  '**/*S3*',
                                  '**/MockData*'] + Qdomains)
            }))
        }

        finalizedBy 'jacocoTestCoverageVerification'
    }

    sonarqube {
        properties {
            property "sonar.projectKey", "shared-food"
            property "sonar.organization", "YAPP-19th"
            property "sonar.host.url", "https://sonarcloud.io"
            property "sonar.java.coveragePlugin", "jacoco"
            property "sonar.test.inclusions", "**/*Test.java"
            property "sonar.exclusions", "**/*Test*.*, **/Q*.JAVA"
            property 'sonar.coverage.jacoco.xmlReportPaths', "${buildDir}/reports/jacoco/test/jacocoTestReport.xml"
        }
    }
}